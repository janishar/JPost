plugins {
    id "com.jfrog.bintray" version "1.7"
}

def ver = '1.0.0-snapshot'
group 'com.mindorks'
version ver

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.mindorks'
            artifactId 'jpost'
            version ver
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    publications = ['MyPublication']

    pkg {
        repo = 'mindorks'
        name = 'jpost'

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/janishar/jPost.git'
        labels = ['java', 'communication', 'class', "modular code","pub-sub"]
        publicDownloadNumbers = true
        version {
            name = ver
            desc = 'java class communication library'
            released  = new Date()
            vcsTag = ver
            attributes = ['java': '1.7']
        }
    }
//    filesSpec {
//        from 'build/libs'
//        into 'com/mindorks/jpost' + ver;
//    }
}

repositories {
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

//UPLOAD PROCEDURE
//cd to the library folder having build.gradle
//ex. cd /Users/janisharali/Mindorks/library/jPost/lib
//gradle bintrayUpload
